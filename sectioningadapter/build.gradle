apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

def libraryVersion = "1.2.8-SNAPSHOT"
def libraryGroupId = "dk.shape.library"
def libraryArtifactId = "sectioning-adapter"

android {
    namespace = "com.novasa.sectioningadapter"
    compileSdkVersion = 35

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    publishing {
        singleVariant("release") {
            withJavadocJar()
            withSourcesJar()
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.13.1'

    api 'androidx.recyclerview:recyclerview:1.3.2'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = libraryGroupId
            artifactId = libraryArtifactId
            version = libraryVersion
            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        maven {
            if (libraryVersion.endsWith("-SNAPSHOT")) {
                url = uri("https://shape.jfrog.io/artifactory/shape-libs-snapshot")
            } else {
                url = uri("https://shape.jfrog.io/artifactory/shape-libs-release")
            }
            name = 'shapeArtifactory'
            credentials(PasswordCredentials)
        }
    }
}

//project.afterEvaluate {
//    publishing {
//        publications {
//            release(MavenPublication) {
//                from components.release
//            }
//        }
//    }
//}